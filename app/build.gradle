plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// =====
var isDev = true
var beta = false
var verBuild = 125
var verName = "1.1.4.x.0"
var verBranch = "math_game_item" // use "RELEASE" for releases versions
var verReleaseTime = 1685605804 // in seconds
// =====

android {
    compileSdk 33
    defaultConfig {
        applicationId "com.fazziclay.opentoday" + (isDev ? ".dev" : "") + (beta ? ".beta" : "")
        minSdk 26
        targetSdk 33
        versionCode verBuild
        versionName verName + " (build " + verBuild + (isDev ? " dev" : "") + ")"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        if (isDev) {
            verReleaseTime = (long) (new Date().time / 1000)
        }

        release {
            buildConfigField "long", "VERSION_RELEASE_TIME", verReleaseTime.toString()
            buildConfigField "String", "VERSION_BRANCH", "\"${verBranch}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "long", "VERSION_RELEASE_TIME", verReleaseTime.toString()
            buildConfigField "String", "VERSION_BRANCH", "\"${verBranch}\""
            versionNameSuffix ' (debug)'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'com.fazziclay.opentoday'
}

dependencies {
    // Application
    implementation files("./libs/OpenTodayTelemetry-v2-v11.jar")
    implementation files("./libs/NeoSocket-1.0.jar")
    implementation files("./libs/JavaNeoUtil-1.1.jar")
    implementation 'com.github.martin-stone:hsv-alpha-color-picker-android:3.0.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-ktx:1.10.1'

    // Tests
    androidTestImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    androidTestImplementation 'org.testng:testng:6.9.6'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20220320'
    androidTestImplementation'org.robolectric:robolectric:3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
